plugins {
    id 'java-library'
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow"
    id 'com.palantir.docker'
    id 'docker-compose'
}

application {
    mainClass = "$mainClassName"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Premain-Class': "$mainClassName"
    }
    zip64=true
}
tasks.jar.dependsOn project(':commons').jar
tasks.jar.dependsOn project(':agent').jar

compileTestJava {
    options.compilerArgs += '-parameters'
}

sourceSets {
    // Make the compileOnly dependencies available when compiling/running tests
    test.compileClasspath += configurations.compileClasspath
    test.runtimeClasspath += configurations.compileClasspath
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}

jar.enabled = true
assemble.dependsOn(shadowJar)

dependencies {
    implementation project(':commons')
    implementation project(':agent')

    implementation("org.apache.avro:avro:${avroVersion}")

    implementation("${pulsarGroup}:pulsar-client:${pulsarVersion}")

    compileOnly("org.apache.cassandra:cassandra-all:${cassandra5Version}")
    testCompileOnly("org.apache.cassandra:cassandra-all:${cassandra5Version}")
    testImplementation("org.apache.cassandra:cassandra-all:${cassandra5Version}") {
        exclude group: 'io.netty'
    }

    testImplementation "com.datastax.oss:java-driver-core:${ossDriverVersion}"
    testImplementation "com.datastax.oss:java-driver-query-builder:${ossDriverVersion}"

    testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
    testImplementation project(':testcontainers')

    testRuntimeOnly "org.slf4j:slf4j-api:${slf4jVersion}"
    testRuntimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"
}

test {
    // Add dependency on jar task, since it will be main target for testing
    dependsOn shadowJar

    // Rearrange test classpath, add compiled JAR instead of main classes
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(shadowJar.archiveFile)

    useJUnitPlatform()

    // JVM arguments for Cassandra 5.0 Java 17+ compatibility
    jvmArgs '--add-exports', 'java.base/jdk.internal.misc=ALL-UNNAMED',
           '--add-exports', 'java.management.rmi/com.sun.jmx.remote.internal.rmi=ALL-UNNAMED',
           '--add-exports', 'java.rmi/sun.rmi.registry=ALL-UNNAMED',
           '--add-exports', 'java.rmi/sun.rmi.server=ALL-UNNAMED',
           '--add-exports', 'java.sql/java.sql=ALL-UNNAMED',
           '--add-exports', 'java.base/java.lang.ref=ALL-UNNAMED',
           '--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED',
           '--add-opens', 'java.base/java.lang.module=ALL-UNNAMED',
           '--add-opens', 'java.base/jdk.internal.loader=ALL-UNNAMED',
           '--add-opens', 'java.base/jdk.internal.ref=ALL-UNNAMED',
           '--add-opens', 'java.base/jdk.internal.reflect=ALL-UNNAMED',
           '--add-opens', 'java.base/jdk.internal.math=ALL-UNNAMED',
           '--add-opens', 'java.base/jdk.internal.module=ALL-UNNAMED',
           '--add-opens', 'java.base/jdk.internal.util.jar=ALL-UNNAMED',
           '--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED',
           '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
           '--add-opens', 'java.base/java.io=ALL-UNNAMED',
           '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
           '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
           '--add-opens', 'java.base/java.util=ALL-UNNAMED',
           '--add-opens', 'java.base/java.nio=ALL-UNNAMED'

    environment 'PULSAR_IMAGE', testPulsarImage + ':' + testPulsarImageTag
    environment 'CASSANDRA_IMAGE', 'cassandra:' + cassandra5Version

    systemProperty "buildDir", buildDir
    systemProperty "projectVersion", project.version
}

dockerPrepare.dependsOn(jar, shadowJar)
dockerPrepare.duplicatesStrategy = DuplicatesStrategy.INCLUDE
docker {
    name "${dockerRepo}cassandra:${cassandra5Version}-cdc"
    files "$buildDir/libs/", "src/docker/"
    buildArgs([BUILD_VERSION: project.version,
               CASSANDRA_VERSION: cassandra5Version,
               COMMITMOG_SYNC_PERIOD_IN_MS: commitlog_sync_period_in_ms,
               CDC_TOTAL_SPACE_IN_MB: cdc_total_space_in_mb])
}
build.dependsOn(project.tasks['docker'])

dockerCompose {
    projectName = "test1"
    useComposeFiles = ['../docker-compose.yaml']
    environment.put 'AGENT_VERSION', 'c5'
    environment.put 'PROJECT_VERSION', project.version
    environment.put 'CASSANDRA_IMAGE', "${dockerRepo}cassandra:${cassandra5Version}-cdc"
    environment.put 'PULSAR_IMAGE', "${dockerRepo}pulsar:${testPulsarImageTag}-csc"
    waitForTcpPorts = false
    startedServices = ['cassandra','pulsar']

    stress {
        projectName = "test1"
        startedServices = ['cassandra-stress']
        scale = ['cassandra-stress': 1]
    }
    stress2 {
        projectName = "test1"
        startedServices = ['cassandra-stress']
        scale = ['cassandra-stress': 2]
    }
    prometheus {
        projectName = "test1"
        startedServices = ['prometheus','grafana']
    }
    elasticsearch {
        projectName = "test1"
        startedServices = ['elasticsearch']
    }
}
