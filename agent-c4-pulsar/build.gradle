plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow"
    id 'com.palantir.docker'
    id 'docker-compose'
}

application {
    mainClassName = "$mainClassName"
}

jar {
    manifest {
        attributes 'Premain-Class': "$mainClassName"
    }
    zip64=true
}

compileTestJava {
    options.compilerArgs += '-parameters'
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}

jar.enabled = false
assemble.dependsOn(shadowJar)

dependencies {
    implementation project(':commons')
    implementation project(':agent')
    implementation project(':agent-luna')
    implementation project(':agent-c4')
    implementation("org.apache.avro:avro:${avroVersion}")
    implementation("org.apache.pulsar:pulsar-client:${pulsarVersion}")

    compileOnly("org.apache.cassandra:cassandra-all:${cassandra4Version}")
    testCompileOnly("org.apache.cassandra:cassandra-all:${cassandra4Version}")

    testImplementation("org.apache.cassandra:cassandra-all:${cassandra4Version}") {
        exclude group: 'io.netty'
    }

    testImplementation "com.datastax.oss:java-driver-core:${ossDriverVersion}"
    testImplementation "com.datastax.oss:java-driver-query-builder:${ossDriverVersion}"

    testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
    testImplementation project(':testcontainers')
    testImplementation project(':testcontainers-pulsar')

    testRuntimeOnly "org.slf4j:slf4j-api:${slf4jVersion}"
    testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
}

test {
    // Add dependency on jar task, since it will be main target for testing
    dependsOn shadowJar

    // Rearrange test classpath, add compiled JAR instead of main classes
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(shadowJar.archiveFile)

    useJUnitPlatform()

    systemProperty "buildDir", buildDir
    systemProperty "projectVersion", project.version
    systemProperty "pulsarVersion", pulsarVersion
}

docker {
    name "${dockerRepo}cassandra:${cassandra4Version}-cdc-pulsar"
    dependsOn shadowJar
    files "$buildDir/libs/", "src/docker/"
    buildArgs([BUILD_VERSION: project.version, CASSANDRA_VERSION: cassandra4Version, COMMITMOG_SYNC_PERIOD_IN_MS: commitlog_sync_period_in_ms])
}
build.dependsOn(project.tasks['docker'])

dockerCompose {
    useComposeFiles = ['../docker-compose.yaml']
    environment.put 'AGENT_VERSION', 'c4'
    environment.put 'STREAM_PLATFORM', 'luna'
    environment.put 'PROJECT_VERSION', project.version
    environment.put 'CASSANDRA_IMAGE', "cassandra:${cassandra4Version}-cdc-pulsar"
    environment.put 'PULSAR_IMAGE', "lunastreaming:${lunaTag}-csc"
    waitForTcpPorts = false
    startedServices = ['cassandra','pulsar']

    stress {
        projectName = "test1"
        startedServices = ['cassandra-stress']
        scale = ['cassandra-stress': 1]
    }
    stress2 {
        projectName = "test1"
        startedServices = ['cassandra-stress']
        scale = ['cassandra-stress': 2]
    }
    prometheus {
        projectName = "test1"
        startedServices = ['prometheus','grafana']
    }
    elasticsearch {
        projectName = "test1"
        startedServices = ['elasticsearch']
    }
}
