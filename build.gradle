plugins {
    id 'java'
    id 'idea'
    id 'application'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "net.ltgt.apt-idea" version "0.20"
    id "com.dorongold.task-tree" version "1.3.1"
    id "jacoco"
    id "com.gorylenko.gradle-git-properties" version "2.2.0"
    id "com.google.osdetector" version "1.6.2"
    id "kr.motd.sphinx" version "2.6.1"
    id "com.bmuschko.docker-remote-api" version "6.7.0"
    id "com.github.hierynomus.license" version"0.15.0"
    id "com.github.hierynomus.license-report" version"0.15.0"
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter() // or Maven central, required for Lombok dependency
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://packages.confluent.io/maven/" }
        maven { url "https://repo.typesafe.com/typesafe/releases/" }
        maven { url "${pulsarRepoUrl}" }
    }

    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.osdetector'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'com.gorylenko.gradle-git-properties'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.1'
    }

    // use JUnit 5 platform
    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
        }
    }

    jacoco {
        toolVersion = "0.8.6"
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
        options.compilerArgs = [ '-parameters', '-Xlint:all', '-Xlint:-processing', '-Xlint:-serial', '-Werror']
    }

    compileTestJava {
        options.compilerArgs += '-parameters'
    }
}

subprojects {

    configurations {
        testAgent {
            transitive = false
        }
    }

    license {
        header rootProject.file('LICENSE-HEADER.txt')
        strictCheck true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        excludes([ "**/cassandra.yaml",
                   "**/logback*",
                   'source/src/main/resources/cassandra-source-version.properties',
                   '**/source-cassandra-*'
        ])
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials(PasswordCredentials) {
                    username project.hasProperty("repoUsername") ? getProperty("repoUsername") : ""
                    password project.hasProperty("repoPassword") ? getProperty("repoPassword") : ""
                }
            }
        }
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
